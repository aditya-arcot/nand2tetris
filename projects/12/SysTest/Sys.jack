// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        // Memory
        do Memory.init();

        // Math
        do Math.init();

        // Output
        do Output.init();

        // Screen
        do Screen.init();

        // Keyboard
        do Keyboard.init();

        // Array - no init
        // String - no init

        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i, j;
        let i = 0;
        while (i < duration){
            let j = 0;
            while (j < 250){ // system specific
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printInt(errorCode);
        do Output.println();
        do Sys.halt();
        return;
    }
}
